// Copyright (c) 2025, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
//
// WSO2 Inc. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
//  http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

import icp_server.types as types;

import ballerina/log;
import ballerina/sql;
import ballerina/uuid;

// Helper function to get Project Admin role ID
isolated function getProjectAdminRoleId() returns string|error {
    sql:ParameterizedQuery query = `SELECT role_id FROM roles_v2 WHERE role_name = 'Project Admin' ORDER BY role_id`;
    query = appendLimitClause(query, 1);

    stream<record {|string role_id;|}, sql:Error?> roleStream = dbClient->query(query);

    record {|string role_id;|}[] roles = check from record {|string role_id;|} role in roleStream
        select role;

    if roles.length() == 0 {
        return error("Project Admin role not found in database");
    }

    return roles[0].role_id;
}

// Create a new project in the projects table (RBAC v2)
public isolated function createProject(types:ProjectInput project, types:UserContextV2 userContext) returns types:Project|error? {
    string projectId = uuid:createType1AsString();
    string userId = userContext.userId;
    string displayName = userContext.displayName;

    if project.name.trim() == "" {
        log:printWarn("Project creation attempted with empty name");
        return error("Project name is required");
    }

    string handler = project.projectHandler.trim();
    if handler == "" {
        log:printWarn("Project creation attempted without handler for project: " + project.name);
        return error("Project handler is required");
    }

    // Check for duplicate handler within the same org
    sql:ParameterizedQuery handlerCheckQuery = `SELECT COUNT(*) as cnt FROM projects WHERE org_id = ${project.orgId} AND handler = ${handler}`;
    stream<record {|int cnt;|}, sql:Error?> handlerCheckStream = dbClient->query(handlerCheckQuery);
    record {|int cnt;|}[] handlerCheckResult = check from record {|int cnt;|} r in handlerCheckStream
        select r;
    if handlerCheckResult.length() > 0 && handlerCheckResult[0].cnt > 0 {
        return error(string `Project handler '${handler}' is already taken in this organization`);
    }

    // Convert deployment pipeline IDs array to JSON string if provided
    string? deploymentPipelineIdsJson = ();
    string[]? pipelineIds = project?.deploymentPipelineIds;
    if pipelineIds is string[] {
        deploymentPipelineIdsJson = pipelineIds.toJsonString();
    }

    transaction {
        // Insert project with all new fields
        sql:ParameterizedQuery insertQuery = `INSERT INTO projects (
            project_id, org_id, name, version, handler, region, description,
            default_deployment_pipeline_id, deployment_pipeline_ids, type,
            git_provider, git_organization, repository, branch, secret_ref,
            owner_id, created_by
        ) VALUES (
            ${projectId}, ${project.orgId}, ${project.name}, ${project?.version}, 
            ${handler}, ${project?.region}, ${project?.description},
            ${project?.defaultDeploymentPipelineId}, ${deploymentPipelineIdsJson}, ${project?.'type},
            ${project?.gitProvider}, ${project?.gitOrganization}, ${project?.repository}, 
            ${project?.branch}, ${project?.secretRef}, ${userId}, ${displayName}
        )`;
        sql:ExecutionResult _ = check dbClient->execute(insertQuery);

        log:printInfo(string `Created project: ${project.name}`,
                projectId = projectId,
                orgId = project.orgId,
                handler = handler,
                ownerId = userId,
                createdBy = displayName);

        // RBAC v2: Create project-specific admin group and assign Project Admin role
        // Errors from the RBAC setup are handled individually so that a duplicate
        // group name is not misreported as a duplicate project name.

        // 1. Create project admin group
        string adminGroupId = uuid:createType1AsString();
        string groupName = string `${handler} Admins`;
        string groupDescription = string `Admin group for project: ${handler}`;

        sql:ExecutionResult|sql:Error groupResult = dbClient->execute(`
            INSERT INTO user_groups (group_id, group_name, org_uuid, description)
            VALUES (${adminGroupId}, ${groupName}, ${project.orgId}, ${groupDescription})
        `);
        if groupResult is sql:Error {
            log:printError(string `Failed to create admin group for project: ${project.name}`, 'error = groupResult);
            fail error("Failed to set up project admin group. Please contact your administrator.", groupResult);
        }
        log:printInfo(string `Created project admin group: ${groupName}`,
                groupId = adminGroupId,
                projectId = projectId);

        // 2. Get Project Admin role ID
        string projectAdminRoleId = check getProjectAdminRoleId();

        // 3. Map group to Project Admin role (project-scoped, all environments)
        sql:ExecutionResult|sql:Error roleMappingResult = dbClient->execute(`
            INSERT INTO group_role_mapping (group_id, role_id, org_uuid, project_uuid)
            VALUES (${adminGroupId}, ${projectAdminRoleId}, ${project.orgId}, ${projectId})
        `);
        if roleMappingResult is sql:Error {
            log:printError(string `Failed to assign admin role for project: ${project.name}`, 'error = roleMappingResult);
            fail error("Failed to set up project admin role. Please contact your administrator.", roleMappingResult);
        }
        log:printInfo(string `Mapped group to Project Admin role for project`,
                groupId = adminGroupId,
                roleId = projectAdminRoleId,
                projectId = projectId);

        // 4. Add creator to admin group
        sql:ExecutionResult|sql:Error userMappingResult = dbClient->execute(`
            INSERT INTO group_user_mapping (group_id, user_uuid)
            VALUES (${adminGroupId}, ${userId})
        `);
        if userMappingResult is sql:Error {
            log:printError(string `Failed to add creator to admin group for project: ${project.name}`, 'error = userMappingResult);
            fail error("Failed to add user to project admin group. Please contact your administrator.", userMappingResult);
        }
        log:printInfo(string `Added project creator to admin group`,
                userId = userId,
                groupId = adminGroupId,
                projectId = projectId);

        check commit;
        log:printInfo(string `Successfully created project and assigned admin roles`,
                projectId = projectId,
                owner = displayName);
    } on fail error e {
        log:printError(string `Failed to create project: ${project.name}`, 'error = e);
        // Only the project INSERT uses `check`, so sql:Error here is project-specific.
        // RBAC setup errors arrive as plain errors with their own messages via `fail`.
        if e is sql:Error {
            match classifySqlError(e) {
                DUPLICATE_KEY => {
                    return error("A project with this name or handler already exists in this organization", e);
                }
                VALUE_TOO_LONG => {
                    return error("The provided value exceeds the maximum allowed length", e);
                }
                FOREIGN_KEY_VIOLATION => {
                    return error("Cannot complete the operation due to a dependency constraint", e);
                }
                _ => {
                    return error("An unexpected error occurred. Please contact your administrator.", e);
                }
            }
        }
        return e;
    }

    return getProjectById(projectId);
}

// Get all projects
public isolated function getProjects() returns types:Project[]|error {
    types:Project[] projects = [];

    sql:ParameterizedQuery query = `SELECT project_id, org_id, name, version, created_date, handler, region, 
                                          description, 
                                          type, git_provider, git_organization, repository, branch, secret_ref,
                                          owner_id, created_by, updated_at, updated_by 
                                   FROM projects 
                                   ORDER BY name ASC`;

    stream<types:Project, sql:Error?> projectStream = dbClient->query(query);

    check from types:Project projectRecord in projectStream
        do {
            projects.push({
                ...projectRecord
            });
        };

    log:printDebug("Retrieved all projects", projectCount = projects.length());

    return projects;
}

// Get projects by specific project IDs with optional org filter (for RBAC v2 filtering)
public isolated function getProjectsByIds(string[] projectIds, int? orgId = ()) returns types:Project[]|error {
    // Return empty array if no project IDs provided
    if projectIds.length() == 0 {
        return [];
    }

    // Safety check: log warning if project ID list is unexpectedly large
    if projectIds.length() > 5000 {
        log:printWarn(string `Large project ID list: ${projectIds.length()} projects - consider pagination`);
    }

    types:Project[] projects = [];

    // Build WHERE clause to filter by project IDs
    sql:ParameterizedQuery query = `SELECT project_id, org_id, name, version, created_date, handler, region, 
                                          description,   
                                          type, git_provider, git_organization, repository, branch, secret_ref,
                                          owner_id, created_by, updated_at, updated_by 
                                     FROM projects 
                                     WHERE project_id IN (`;

    // Add project IDs to the IN clause
    foreach int i in 0 ..< projectIds.length() {
        if i > 0 {
            query = sql:queryConcat(query, `, `);
        }
        query = sql:queryConcat(query, `${projectIds[i]}`);
    }

    query = sql:queryConcat(query, `)`);

    // Add orgId filter if provided
    if orgId is int {
        query = sql:queryConcat(query, ` AND org_id = ${orgId}`);
    }

    query = sql:queryConcat(query, ` ORDER BY name ASC`);

    stream<types:Project, sql:Error?> projectStream = dbClient->query(query);

    check from types:Project projectRecord in projectStream
        do {
            projects.push({
                ...projectRecord
            });
        };

    log:printDebug("Retrieved projects by IDs",
            projectCount = projects.length(),
            requestedIds = projectIds.length(),
            orgIdFilter = orgId);

    return projects;
}

// Get a specific project by ID
public isolated function getProjectById(string projectId) returns types:Project|error {
    stream<types:Project, sql:Error?> projectStream =
        dbClient->query(`SELECT project_id, org_id, name, version, created_date, handler, region, 
                                description, 
                                type, git_provider, git_organization, repository, branch, secret_ref,
                                owner_id, created_by, updated_at, updated_by 
                         FROM projects WHERE project_id = ${projectId}`);

    types:Project[] projectRecords =
        check from types:Project projectRecord in projectStream
        select projectRecord;

    if projectRecords.length() == 0 {
        return error(string `Project with ID ${projectId} not found`);
    }

    types:Project projectRecord = projectRecords[0];
    return projectRecord;
}

// Get project ID by handler
public isolated function getProjectIdByHandler(string projectHandler, int orgId) returns string|error {
    stream<record {|string project_id;|}, sql:Error?> projectStream = dbClient->query(`
        SELECT project_id FROM projects WHERE handler = ${projectHandler} AND org_id = ${orgId} 
    `);

    record {|string project_id;|}[] projectRecords = check from record {|string project_id;|} project in projectStream
        select project;

    if projectRecords.length() == 0 {
        return error(string `Project ${projectHandler} not found.`);
    }
    return projectRecords[0].project_id;
}

// Get project handler by project ID
public isolated function getProjectHandlerById(string projectId) returns string|error {
    stream<record {|string handler;|}, sql:Error?> projectStream = dbClient->query(`
        SELECT handler FROM projects WHERE project_id = ${projectId}
    `);

    record {|string handler;|}[] projectRecords = check from record {|string handler;|} project in projectStream
        select project;

    if projectRecords.length() == 0 {
        return error(string `Project with ID ${projectId} not found`);
    }
    return projectRecords[0].handler;
}

// Update project with ProjectUpdateInput
public isolated function updateProjectWithInput(types:ProjectUpdateInput project) returns error? {
    sql:ParameterizedQuery whereClause = ` WHERE project_id = ${project.id} `;
    sql:ParameterizedQuery updateFields = ` SET updated_at = CURRENT_TIMESTAMP `;
    boolean hasUpdates = false;

    if project?.name is string {
        updateFields = sql:queryConcat(updateFields, `, name = ${project?.name} `);
        hasUpdates = true;
    }
    if project?.description is string {
        updateFields = sql:queryConcat(updateFields, `, description = ${project?.description} `);
        hasUpdates = true;
    }
    if project?.version is string {
        updateFields = sql:queryConcat(updateFields, `, version = ${project?.version} `);
        hasUpdates = true;
    }
    if project?.orgId is int {
        updateFields = sql:queryConcat(updateFields, `, org_id = ${project?.orgId} `);
        hasUpdates = true;
    }

    if !hasUpdates {
        return error("No fields to update");
    }

    transaction {
        // Update the project
        sql:ParameterizedQuery updateQuery = sql:queryConcat(`UPDATE projects `, updateFields, whereClause);
        sql:ExecutionResult _ = check dbClient->execute(updateQuery);

        check commit;
        log:printInfo(string `Successfully updated project ${project.id}`);
    } on fail error e {
        log:printError(string `Failed to update project ${project.id}`, 'error = e);
        if e is sql:Error {
            match classifySqlError(e) {
                DUPLICATE_KEY => {
                    return error("A project with this name already exists in this organization", e);
                }
                VALUE_TOO_LONG => {
                    return error("The provided value exceeds the maximum allowed length", e);
                }
                _ => {
                    return error("An unexpected error occurred. Please contact your administrator.", e);
                }
            }
        }
        return error("An unexpected error occurred while updating the project. Please contact your administrator.", e);
    }

    return ();
}

// Delete a project by ID and clean up the auto-created project admin group.
public isolated function deleteProject(string projectId) returns error? {
    do {
        transaction {
            record {|string name; string handler; int org_id;|}|sql:Error projectRecord = dbClient->queryRow(
                `SELECT name, handler, org_id FROM projects WHERE project_id = ${projectId}`
            );
            if projectRecord is sql:NoRowsError {
                fail error(string `Project with ID ${projectId} not found`);
            }
            if projectRecord is sql:Error {
                fail projectRecord;
            }

            string projectAdminRoleId = check getProjectAdminRoleId();
            string handlerBasedAdminGroup = string `${projectRecord.handler} Admins`;

            sql:ExecutionResult|sql:Error groupDeleteResult = dbClient->execute(`
                DELETE FROM user_groups
                WHERE group_name = ${handlerBasedAdminGroup}
                    AND org_uuid = ${projectRecord.org_id}
                    AND group_id IN (
                        SELECT group_id FROM group_role_mapping
                        WHERE project_uuid = ${projectId} AND role_id = ${projectAdminRoleId}
                    )
            `);
            if groupDeleteResult is sql:Error {
                fail groupDeleteResult;
            }

            // Explicitly remove all remaining group_role_mapping rows scoped to this project.
            // On MySQL/H2/PostgreSQL these would be cascade-deleted when the project row is removed,
            // but MSSQL defines fk_grp_role_project as ON DELETE NO ACTION (to avoid multiple cascade
            // path errors), so it does not cascade and instead leaves orphaned records silently.
            // Deleting them here makes the cleanup correct and explicit on all database engines.
            sql:ExecutionResult|sql:Error roleMappingDeleteResult = dbClient->execute(
                `DELETE FROM group_role_mapping WHERE project_uuid = ${projectId}`
            );
            if roleMappingDeleteResult is sql:Error {
                fail roleMappingDeleteResult;
            }
            log:printInfo(string `Removed all role mappings scoped to project`, projectId = projectId);

            sql:ExecutionResult|sql:Error projectDeleteResult = dbClient->execute(
                `DELETE FROM projects WHERE project_id = ${projectId}`
            );
            if projectDeleteResult is sql:Error {
                fail projectDeleteResult;
            }

            check commit;
        }
    } on fail error e {
        log:printError(string `Failed to delete project ${projectId}`, 'error = e);
        if e is sql:Error {
            match classifySqlError(e) {
                FOREIGN_KEY_VIOLATION => {
                    return error("Cannot delete project because it has dependent resources", e);
                }
                _ => {
                    return error("An unexpected error occurred. Please contact your administrator.", e);
                }
            }
        }
        return e;
    }

    log:printInfo(string `Successfully deleted project ${projectId}`);
    return ();
}

// Check project handler availability for an organization
public isolated function checkProjectHandlerAvailability(int orgId, string projectHandlerCandidate) returns types:ProjectHandlerAvailability|error {
    log:printDebug(string `Checking project handler availability for orgId: ${orgId}, handler: ${projectHandlerCandidate}`);

    // Check if the handler already exists for this organization
    sql:ParameterizedQuery query = `SELECT COUNT(*) as HANDLECOUNT 
                                   FROM projects 
                                   WHERE org_id = ${orgId} AND handler = ${projectHandlerCandidate}`;

    int existingHandlerCount = 0;

    stream<record {}, sql:Error?> handlerCountStream = dbClient->query(query);

    check from record {} countRecord in handlerCountStream
        do {
            existingHandlerCount = <int>countRecord["HANDLECOUNT"];
        };

    boolean isHandlerUnique = existingHandlerCount == 0;
    string? alternateCandidate = ();

    // If handler is not unique, generate an alternate candidate
    if !isHandlerUnique {
        // Generate alternate handler suggestions by appending numbers
        int counter = 1;
        string baseHandler = projectHandlerCandidate;

        while counter <= 10 { // Limit to 10 attempts to avoid infinite loop
            string candidate = string `${baseHandler}${counter}`;

            sql:ParameterizedQuery alternateQuery = `SELECT COUNT(*) as HANDLECOUNT 
                                                   FROM projects 
                                                   WHERE org_id = ${orgId} AND handler = ${candidate}`;

            int candidateCount = 0;
            stream<record {}, sql:Error?> candidateStream = dbClient->query(alternateQuery);

            check from record {} candidateRecord in candidateStream
                do {
                    candidateCount = <int>candidateRecord["HANDLECOUNT"];
                };

            if candidateCount == 0 {
                alternateCandidate = candidate;
                break;
            }

            counter += 1;
        }
    }

    log:printDebug(string `Project handler availability check completed`,
            orgId = orgId,
            projectHandlerCandidate = projectHandlerCandidate,
            isHandlerUnique = isHandlerUnique,
            alternateCandidate = alternateCandidate);

    return {
        handlerUnique: isHandlerUnique,
        alternateHandlerCandidate: alternateCandidate
    };
}

